#!/usr/bin/env perl
use v5.26;
use strict;
use warnings;

# This is like pod2github or pod2markdown, but takes a list of filename(s)
# as arguments and outputs markdown documents in the right location(s)
# using the right arguments for Conch.

# Usage:
# misc/pod2githubpages <filename> [filename ... ]

use Pod::Markdown::Github;
use Devel::Confess;

my %opts = (
    output_encoding => 'UTF-8',
    local_module_re => qr/Conch/,
    local_module_url_prefix => '/conch/modules/',   # '' is better but Pod::Markdown doesn't respect it
);

my @markdown_files;

while (my $infile = shift @ARGV) {
    # convert:                          lib/Conch/Time.pm
    # to:                               doc/modules/Conch::Time.md
    # containing pod links like:        L<Conch::Time>
    # become relative URLs like:        /modules/Conch::Time
    # that turn into the absolute url:  https://joyent.github.io/conch/modules/Conch::Time

    my $outfile = $infile;
    $outfile =~ s{^lib/}{};
    $outfile =~ s{/}{::}g;
    $outfile =~ s/\.pm$/\.md/;
    $outfile =~ s{^lib::}{};
    push @markdown_files, $outfile;
    $outfile = 'docs/modules/'.$outfile;

    my $in_fh  = get_handle($infile, '<', \*STDIN);
    my $out_fh = get_handle($outfile, '>', \*STDOUT);

    # Undo any PERL_UNICODE effects.
    # Pod::Simple expects to receive bytes, and we're going to return bytes.
    binmode($_, ':bytes') for ($in_fh, $out_fh);

    convert($in_fh, $out_fh);
}

{
    open my $fh, '>', 'docs/modules/index.md'
        or die "cannot open docs/modules/index.md for writing: $!";

    print $fh "# Module documentation\n\n";

    foreach my $filename (sort @markdown_files) {
        $filename =~ s/.md$//;
        my $module = $filename =~ s/_/\\_/gr;
        print $fh '* [', $module, '](', $filename, ")\n";
    }

    print $fh <<'FOOTER';

# Copyright / License

Copyright Joyent, Inc.

This Source Code Form is subject to the terms of the Mozilla Public License,
v.2.0. If a copy of the MPL was not distributed with this file, you can
obtain one at <http://mozilla.org/MPL/2.0/>.
FOOTER
    close $fh;
}

sub convert {
    my ($in_file, $out_file) = @_;
    my $parser = Pod::Markdown::Github->new(%opts);
    $parser->output_fh($out_file);
    $parser->parse_file($in_file);
}

sub get_handle {
  my ($path, $op, $default) = @_;
  (!defined($path) || $path eq '-') ? $default : do {
    open(my $fh, $op, $path)
      or die "Failed to open '$path': $!\n";
    $fh;
  };
}

